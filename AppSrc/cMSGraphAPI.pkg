Use UI

Class cMSGraphAPI is a cObject
    
    Procedure Construct_Object
        Forward Send Construct_Object
        
        Property Handle  phoHttp
        Property String  psGraphHost     "graph.microsoft.com"
        Property String  psGraphBasePath "v1.0/"
        Property String  psGraphBetaPath "beta/"
        Property String  psGraphScopes   ""
        Property Boolean pbUseBeta       False
    End_Procedure
    
    Function GraphPath Returns String
        If (pbUseBeta(Self)) ;
            Function_Return (psGraphBetaPath(Self))
        Else ;
            Function_Return (psGraphBasePath(Self))
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/user-list-events?view=graph-rest-1.0&tabs=http
    Function ListEvents String sCalendar String sParams Returns Handle
        Handle hoEvents
        String sPath
        
        If (sCalendar = "") ;
            Move (GraphPath(Self) + "me/events") to sPath
        Else ;
            Move (GraphPath(Self) + "me/calendars/" + sCalendar + "/events") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoEvents
        Function_Return hoEvents
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/event-list-attachments?view=graph-rest-1.0&tabs=http
    Function ListEventAttachments String sID String sParams Returns Handle
        String sPath
        Handle hoAtts
        
        Move (GraphPath(Self) + "me/events/" + sID + "/attachments") to sPath
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoAtts
        
        Function_Return hoAtts
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/user-sendmail?view=graph-rest-1.0&tabs=http
    Function SendMail Handle hoMessage String sParams Returns Handle
        String sPath
        Handle hoResp
        
        Move (GraphPath(Self) + "me/sendMail") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "POST" sPath sParams hoMessage to hoResp  // Will be -1 if OK, 0 if not
        
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/user-post-messages?view=graph-rest-1.0&tabs=http
    Function CreateDraftMail Handle hoMessage String sParams Returns Handle
        String sPath
        Handle hoResp
        
        Move (GraphPath(Self) + "me/messages") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "POST" sPath sParams hoMessage to hoResp
        
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/user-list-contacts?view=graph-rest-1.0&tabs=http
    Function ListContacts String sParams Returns Handle
        String sPath
        Handle hoResp
        
        Move (GraphPath(Self) + "me/contacts") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoResp
        
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/user-list-mailfolders?view=graph-rest-1.0&tabs=http
    Function ListMailFolders String sParams Returns Handle
        String sPath
        Handle hoResp
        
        Move (GraphPath(Self) + "me/mailFolders") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoResp
                
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/user-list-messages?view=graph-rest-1.0&tabs=http
    Function ListMessages String sFolder String sParams Returns Handle
        String sPath
        Handle hoResp
        
        If (sFolder = "") ;
            Move (GraphPath(Self) + "me/messages") to sPath
        Else ;
            Move (GraphPath(Self) + "me/mailFolders/" + sFolder + "/messages") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoResp
        
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/message-list-attachments?view=graph-rest-1.0&tabs=http
    Function ListMailAttachments String sMsgID String sParams Returns Handle
        Handle hoResp
        String sPath
        
        Move (GraphPath(Self) + "me/messages/" + sMsgID + "/attachments") to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoResp
        
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/attachment-get?view=graph-rest-1.0&tabs=http
    Function Attachment String sMsgID String sAttID String sParams Returns Handle
        Handle hoResp
        String sPath
        
        Move (GraphPath(Self) + "me/messages/" + sMsgID + "/attachments/" + sAttID) to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams 0 to hoResp
        
        Function_Return hoResp
    End_Function
    
    // See: https://learn.microsoft.com/en-us/graph/api/message-get?view=graph-rest-1.0&tabs=http
    Function GetMessage String sID String sParams Returns Handle
        Handle hoResp
        String sPath
        
        Move (GraphPath(Self) + "me/messages/" + sID) to sPath
        
        Get MakeJsonCall of (phoHttp(Self)) "GET" sPath sParams to hoResp
        
        Function_Return hoResp
    End_Function
    
        
End_Class